apply plugin: 'com.android.application'


android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    // note: use gradle.properties in project root level to set your api credentials
    def apiKey = project.properties['indoorAtlasApiKey'] ?: "api-key-not-set";
    def apiSecret = project.properties['indoorAtlasApiSecret'] ?: "api-secret-not-set";
    def pubNubPublishKey = project.properties['pubNubPublishKey'] ?: "not-set";
    def pubNubSubscribeKey = project.properties['pubNubSubscribeKey'] ?: "not-set";


    defaultConfig {
        applicationId "com.indooratlas.android.sdk.examples"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        // avoid getting errors from malformed string resources if secret contains '%' chars
        resValue "string", "indooratlas_api_key", apiKey
        resValue "string", "indooratlas_api_secret", apiSecret.replaceAll("%", "\\\\u0025")
        resValue "string", "pubnub_publish_key", pubNubPublishKey
        resValue "string", "pubnub_subscribe_key", pubNubSubscribeKey
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "sdk_debug"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.indooratlas.android:indooratlas-android-sdk:2.7.0-beta-848@aar"
    compile 'com.indooratlas.android:indooratlas-android-wayfinding:2.7.0-beta-9'

    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.google.android.gms:play-services-maps:8.1.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.+'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.2.0'
    compile 'com.pubnub:pubnub-android:3.7.5'
    compile 'com.android.support:design:23.0.1'

    // for Open Street Map support
    compile 'org.osmdroid:osmdroid-android:5.6.4'
    compile 'com.github.MKergall:osmbonuspack:6.2'
}
